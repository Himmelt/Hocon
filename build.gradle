plugins {
    id "com.jfrog.bintray" version "1.8.0"
    id "com.jfrog.artifactory" version "4.9.3"
}

apply plugin: 'java'
apply plugin: 'maven-publish'

group = 'org.soraworld'
version = '1.2.1'.concat(isMasterBranch() ? '' : '-SNAPSHOT')
archivesBaseName = "Hocon"

println 'Current Branch  ' + gitBranch()
println 'Current Version ' + project.version

def _BINTRAY_USER = System.getenv('BINTRAY_USER') != null ? System.getenv('BINTRAY_USER') : BINTRAY_USER
def _BINTRAY_KEY = System.getenv('BINTRAY_KEY') != null ? System.getenv('BINTRAY_KEY') : BINTRAY_KEY

sourceCompatibility = targetCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
    testCompile 'junit:junit:4.12'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

javadoc {
    options.encoding = 'UTF-8'
    options.charSet = 'UTF-8'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = 'org.soraworld'
            artifactId = 'hocon'
            version = project.version
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    key = _BINTRAY_KEY
    user = _BINTRAY_USER
    publications = ['mavenJava']
    publish = true
    override = true
    pkg {
        repo = 'Minecraft'
        name = 'Hocon'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/Himmelt/Hocon.git'
        publicDownloadNumbers = true
        version {
            name = project.version
            desc = 'Simple Hocon Configuration Library.'
            released = new Date()
        }
    }
}

artifactory {
    contextUrl = 'https://oss.jfrog.org/artifactory'
    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = _BINTRAY_USER
            password = _BINTRAY_KEY
            maven = true
        }
        defaults {
            publications('mavenJava')
        }
    }
}

static String gitBranch() {
    def info = 'git branch'.execute().text.trim()
    def version = 'unknown'
    info.eachLine { line -> if (line.contains('*')) version = line.trim() }
    return version
}

static boolean isMasterBranch() {
    return gitBranch().contains('* master')
}

tasks.bintrayUpload.enabled = isMasterBranch()
tasks.artifactoryPublish.enabled = !isMasterBranch()